(*Anonymous Authors*)

(*--Dolev-Yao model Open Channels--*)
free c:channel. (*Public Channel between UAVs*)

free posA: bitstring [private].
weaksecret posA.
free posB: bitstring [private].
weaksecret posB.

(*--Types--*)
type host.
type alt.
type lon.
type lat.
type sk.
type ts.
type G [large,bounded].

(*--Shared Symmetric Key--*)
free sym: sk [private].

(*--UAVs--*)
free uA,uB: host.

(* Auxiliary Functions *)
fun mul(G,G):G.
fun mul2(bitstring,G):G.
fun inverse_mod(G,G):G.
fun sub(G,G):G.
fun div(G,G):G.
fun cast2bs(G):bitstring.
fun bs2g(bitstring):G.
fun map(lat, lon, alt):bitstring.

fun exp(bitstring,G):G.
fun exp2(G,G):G.

fun hash(G):bitstring.

(* Symmetric Encryption *)
fun senc (bitstring, sk):bitstring.
reduc forall m:bitstring, k:sk; sdec(senc(m,k),k) = m.

(*Events*)
event acceptUAVa(sk,host).
event acceptUAVb(sk,host).
event termUAVa(sk,host).
event termUAVb(sk,host).

(*--Check timestamp freshness operation--*)
fun freshness(ts, bool): bool
reduc forall T: ts; freshness(T, true) = true
otherwise forall T: ts; freshness(T, false) = false.

(* Authentication *)
(*UAV A authenticates UAV B*)
query x:sk, y:host; inj-event(termUAVa(x,y))==>inj-event(acceptUAVb(x,y)).

(*UAV B authenticates UAV A*)
query x:sk, y:host; inj-event(termUAVb(x,y))==>inj-event(acceptUAVa(x,y)).


(* Test if posA is secret *)
query attacker(posA).
query attacker(posB).

(* UAV Client *)
let uavA () = 
  
  new oC:G;  (*value: 1*)
  new exC:G; (*exponent value: 3*)
  new pC:G;
  new qC:G;

  let nc    = mul(pC,qC) in
  let fic   = mul(sub(pC,oC),sub(qC,oC)) in
  let ec    = exC in
  let dc    = inverse_mod(ec,fic) in

  in(c, ((nS:G, eS:G),X:host));
  
  if X<>uA then (

  event acceptUAVb(sym, X);

  out(c, ((nc, ec),uA));
  (* Preparation *)
  new Rci:G;
  new latB: lat;
  new lonB: lon;
  new altB: alt;
  new tB: ts;
  let posB  = map(latB, lonB, altB) in

  let yC = mul2(posB, exp2(Rci,eS)) in
  
  out(c, (senc(cast2bs(yC),sym), tB, freshness(tB, true)));

  in(c, ((ys_enc:bitstring, tjS_enc:bitstring, tA:ts, checkT:bool)));

  if checkT = true then
    let ys = bs2g(sdec(ys_enc,sym)) in
    let tjS = sdec(tjS_enc,sym) in

    let kci = div(ys,Rci) in
    let ti = hash(kci) in

    if ti = tjS then
      event termUAVa(sym, X)
  ).

(* UAV Server *)
let uavB () = 
  new o:G;  (*value: 1*)
  new ex:G; (*exponent value: 3*)
  new p:G;
  new q:G;
  new tA:ts;

  let n   = mul(p,q) in
  let fi  = mul(sub(p,o),sub(q,o)) in
  let e   = ex in
  let d   = inverse_mod(e,fi) in
  out(c, ((n,e), uB));

  in(c, ((nC:G, eC:G),Y:host));
  
  if Y<>uB then (

  event acceptUAVa(sym, Y);

  (* Preparation *)
  new latA: lat;
  new lonA: lon;
  new altA: alt;
  let posA  = map(latA, lonA, altA) in

  let ksj = exp(posA,d) in
  let tj = hash(ksj) in

  in(c, (yc_enc:bitstring, tB:ts, checkT:bool));

  if checkT = true then
    let yc_dec = bs2g(sdec(yc_enc,sym)) in
    let yS = exp2(yc_dec,d) in
  
    (* out(c, (yS, tj)). *)
    out(c, (senc(cast2bs(yS),sym), senc(tj,sym), tB, freshness(tA, true)));
    event termUAVb(sym, Y)
  ).

let mys =
  ! (uavA() | uavB()).
  
process mys

(*--------------------------------------------------------------
Verification summary:

RESULT Weak secret posA is true.
RESULT Weak secret posB is true.
RESULT inj-event(termUAVa(x,y)) ==> inj-event(acceptUAVb(x,y)) is true.
RESULT inj-event(termUAVb(x,y)) ==> inj-event(acceptUAVa(x,y)) is true.
RESULT not attacker(posA[]) is true.
RESULT not attacker(posB[]) is true.

-------------------------------------------------------------*)
